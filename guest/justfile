default:
    just --list

build: build-assets
    cargo build --release --target=wasm32-unknown-unknown

build-assets:
    mkdir -p target
    convert assets/static-0001.png -depth 8 rgb:target/static-0001.raw
    convert assets/anim-0001.gif -coalesce -depth 8 rgb:target/anim-0001_%03d.raw
    convert assets/anim-0002.png -depth 8 rgb:target/anim-0002.raw

gallery: build-assets
    #!/bin/bash
    mkdir -p target/tmp
    for f in target/*.raw; do
        name=$(basename "$f" .raw)
        convert -size 16x16 -depth 8 -filter point -resize 800% \
            "rgb:$f" "target/tmp/${name}.png"
    done

    montage -label '%t' target/tmp/*.png \
        -geometry +10+10 \
        -background white -fill black \
        -pointsize 16 \
        target/gallery.png

view-gallery: gallery
    feh target/gallery.png

opt: build
    # Needs: cargo install wasm-opt
    wasm-opt \
        -O3 \
        --strip-debug \
        --strip-producers \
        --vacuum \
        --remove-unused-module-elements \
        --simplify-locals \
        --reorder-locals \
        --coalesce-locals \
        --flatten \
        --local-cse \
        -o target/wasm32-unknown-unknown/release/guest_opt.wasm \
        target/wasm32-unknown-unknown/release/guest.wasm

print: build
    wasm-tools print target/wasm32-unknown-unknown/release/guest.wasm

clean:
    cargo clean

ci: build-assets
    cargo clippy -- -D warnings
    cargo fmt --all -- --check
